import com.github.spotbugs.SpotBugsTask

group 'com.mickey305'
version '0.6.21-beta'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.github.spotbugs'

def defaultEncoding = 'UTF-8'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        //options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:none"
        options.encoding = defaultEncoding
    }
    tasks.withType(Test) {
        systemProperty "file.encoding", defaultEncoding
    }
    // To generate an HTML report instead of XML
    tasks.withType(SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10
}

javadoc {
    options.charSet = defaultEncoding
    options.encoding = defaultEncoding
    options.addBooleanOption('html5', true)
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

spotbugs {
    toolVersion = '3.1.9'
    ignoreFailures = true
}

publish {
    userOrg = 'mickey305'
    groupId = this.getGroup()
    artifactId = 'foundation'
    publishVersion  = this.getVersion()
    desc = 'The "Foundation" library is part of infrastructure component for Java.'
    website = 'https://github.com/mickey305/Foundation'
    licences = ['Apache-2.0']
}

sourceSets {
    // we define `main` sourceSet here, so SpotBugs Gradle Plugin generates `spotbugsMain` task
    main {
        java.srcDirs = ['src/main/java']
    }
}

repositories {
    mavenCentral()
    jcenter()
    //mavenLocal()
}

dependencies {
    // - Maven repository hosting
    //  URL: https://mvnrepository.com/
    testCompile group: 'junit'                   , name: 'junit'               , version: '4.12'
    compile     group: 'org.apache.commons'      , name: 'commons-lang3'       , version: '3.7'
    compile     group: 'org.hibernate'           , name: 'hibernate-validator' , version: '6.0.13.Final'
    compile     group: 'org.glassfish'           , name: 'javax.el'            , version: '3.0.1-b10'
    compile     group: 'org.apache.commons'      , name: 'commons-math3'       , version: '3.6.1'
    compile     group: 'org.apache.commons'      , name: 'commons-collections4', version: '4.2'
    compile     group: 'commons-codec'           , name: 'commons-codec'       , version: '1.11'
    compile     group: 'com.google.code.findbugs', name: 'jsr305'              , version: '3.0.2'
    //compile     group: 'com.github.spotbugs'     , name: 'spotbugs'            , version: '3.1.9'
    //compileOnly group: 'com.github.spotbugs'     , name: 'spotbugs-annotations', version: '3.1.9'

    def jreVersion = Double.parseDouble(System.getProperty("java.specification.version"))
    if (jreVersion >= 9) {
        //compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.11'
    }

    // - Maven repository hosting
    //  URL: https://search.maven.org/artifact/uk.com.robust-it/cloning/
    // -- OSS repository
    //  URL: https://github.com/kostaskougios/cloning
    compile     group: 'uk.com.robust-it'        , name: 'cloning'             , version: '1.9.10'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
}

